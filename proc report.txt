/* The REPORT procedure combines features */
/* of the PRINT, MEANS, and TABULATE procedures */
/* with features of the DATA step in a single report-writing tool */
/* that can produce a variety of reports. You can use PROC REPORT */
/* in the following ways: */

option mlogic mprint symbolgen fmtsearch=(work);
/* libname proclib "SAS-library"; */

data grocery;
input Sector $ Manager $ Department $ Sales @@;
datalines;
se 1 np1 50    se 1 p1 100   se 1 np2 120   se 1 p2 80
se 2 np1 40    se 2 p1 300   se 2 np2 220   se 2 p2 70
nw 3 np1 60    nw 3 p1 600   nw 3 np2 420   nw 3 p2 30
nw 4 np1 45    nw 4 p1 250   nw 4 np2 230   nw 4 p2 73
nw 9 np1 45    nw 9 p1 205   nw 9 np2 420   nw 9 p2 76
sw 5 np1 53    sw 5 p1 130   sw 5 np2 120   sw 5 p2 50
sw 6 np1 40    sw 6 p1 350   sw 6 np2 225   sw 6 p2 80
ne 7 np1 90    ne 7 p1 190   ne 7 np2 420   ne 7 p2 86
ne 8 np1 200   ne 8 p1 300   ne 8 np2 420   ne 8 p2 125
;

proc format library=work;

value $sctrfmt	"se" = "Southeest"
				"ne" = "Northeast"
				"nw" = "Northwest"
				"sw" = "Southwest"
;

value $mgrfmt	'1' = 'Smith'   '2' = 'Jones'
                '3' = 'Reveiz'  '4' = 'Brown'
                '5' = 'Taylor'  '6' = 'Adams'
                '7' = 'Alomar'  '8' = 'Andrews'
                '9' = 'Pelfrey'
;

value $deptfmt 'np1' = 'Paper'
               'np2' = 'Canned'
               'p1'  = 'Meat/Dairy'
               'p2'  = 'Produce'
;
run;

proc report data=grocery;
column manager department sales;
rbreak after / summarize style = [font_weight=bold];
where sector="se";
format manager $mgrfmt. department $deptfmt. sales dollar11.2;
title "Sales for the Southeast Sector";
title2 "&sysdate.";
run;

proc report data=grocery;
column manager department sales;
define manager / order order=formatted format=$mgrfmt.;
define department / order order=internal format=$deptfmt.;
define sales / analysis sum format=dollar7.2;
break after manager /summarize style=[font_style=italic];
where sector="se";
title "Sales for the Southeast Sector";
run;

proc report data=grocery;
column manager department sales
sales=salesmin
sales=salesmax;
define manager / order order=formatted format=$mgrfmt. "Manager";
define department / order order=internal format=$deptfmt. "Department";
define sales / analysis sum format=dollar7.2 "Sales";
define salesmin / analysis min noprint;
define salesmax / analysis max noprint;
compute after;
line "Departmental sales ranged from" salesmin dollar7.2 " " "to " salesmax dollar7.2 " " ;
endcomp;
where sector="se";
title "Sales for the Southeast Sector";
title2 "&sysdate";
run;

proc report data=grocery;
column sector manager (Sum Min Max Range Mean Std),sales;
define manager / group format=$mgrfmt.;
define sector / group format=$sctrfmt.;
define sales / format=dollar11.2;
title "Sales Statistics for All Sectors";
run;

proc report data=grocery;
column sector manager sales;
define sector / group format=$sctrfmt. "Sector";
define manager / group format=$mgrfmt. "Manager";
define sales / analysis sum format=comma10.2 "Sales";
break after sector / summarize style=[font_style=italic] suppress;
compute after;
	line "Combined sales for the northern sectors were " sales.sum dollar9.2 ".";
endcomp;
compute sales;
	if _break_ ne " " then call define(_col_, "format", "dollar11.2");
endcomp;
where sector="n";
title "Sales Figures for Northern Sectors";
run;


proc report data=grocery split='*';
column sector manager department,sales perish;
define sector / group format=$sctrfmt. 'Sector' '';
define manager / group format=$mgrfmt. 'Manager* ';
define department / across format=$deptfmt. 'Department';
define sales / analysis sum format=dollar11.2 ' ';
define perish / computed format=dollar11.2 'Perishable*Total';
compute perish;
	perish=sum(_c3_, _c4_);
endcomp;
compute after;
	line 'Combined sales for meat and dairy : ' _c3_ dollar11.2 '';
    line  'Combined sales for produce : ' _c4_ dollar11.2 '';
	line  'Combined sales for all perishables: ' _c5_ dollar11.2 '';
endcomp;
where sector contains 'n' and (department='p1' or department='p2');
title 'Sales Figures for Perishables in Northern Sectors';
run;

proc report data=grocery;
title "Sales for Indicidual Stores";
column sector manager department sales Profit;
define sector / group noprint;
define manager /group noprint;
define profit / computed format=dollar11.2;
define sales / analysis sum format=dolar11.2;
define department / group format=$deptfmt.;
compute profit;
if department="np1" or department="np2"
	then profit=0.4*sales.sum;
	else profit=0.25*sales.sum;
endcomp;
compute before _page_ / style={just=left};
	line sector $sctrfmt. " Sector";
	line "Store managed by " manager $mgrfmt.;
endcomp;
break after manager / summarize style=[font_style=italic] page;
compute after manager;
	length text $35;
	if sales.sum lt 500 then text="Sales are below the target region.";
	else if sales.sum ge 500 and sales.sum lt 1000 then text="Sales are in the target region.";
	else if sales.sum ge 1000 then text="Sales exceeded goal";
	line text $35.;
endcomp;
run;

proc report data=grocery;
title;
column ("Individual Store Sales as a Percent of All Sales"
sector manager sales,(sum pctsum) comment);
define manager / group format=$mgrfmt.;
define sector / group format=$sctrfmt.;
define sales / format=dollar11.2 "";
define sum / format=dollar9.2 "Total Sales";
define pctsum / "Percent of Sales" format=percent6.;
define comment / computed style(column)=[cellwidth=2.6in];
compute comment / char lenght=40;
if sales.pctsum gt .15 and _break_ = " "
	then comment="Sales substantially above expectations.";
	else comment=" ";
endcomp;
run;

data grocmiss;
   input Sector $ Manager $ Department $ Sales @@;
datalines;
se 1 np1 50    .  1 p1 100   se . np2 120   se 1 p2 80
se 2 np1 40    se 2 p1 300   se 2 np2 220   se 2 p2 70
nw 3 np1 60    nw 3 p1 600   .  3 np2 420   nw 3 p2 30
nw 4 np1 45    nw 4 p1 250   nw 4 np2 230   nw 4 p2 73
nw 9 np1 45    nw 9 p1 205   nw 9 np2 420   nw 9 p2 76
sw 5 np1 53    sw 5 p1 130   sw 5 np2 120   sw 5 p2 50
.  . np1 40    sw 6 p1 350   sw 6 np2 225   sw 6 p2 80
ne 7 np1 90    ne . p1 190   ne 7 np2 420   ne 7 p2 86
ne 8 np1 200   ne 8 p1 300   ne 8 np2 420   ne 8 p2 125
;

proc report data=grocmiss  missing out=data(where=(sales gt 1000 and _break_=""));
column sector manager N sales;
define sector / group format=$sctrfmt.;
define manager / group format=$mgrfmt.;
define sales /format=dollar9.2;
rbreak after / summarize style=[fontstyle=italic];
title "Summary Report for All Sectors and Managers";
run;